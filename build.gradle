plugins {
    id "com.github.hierynomus.license" version "0.15.0"
}

group 'de.johni0702.replay'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
}

ext.projectName = 'ReplayRestrictions'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'
ext.inceptionYear = '2015'

subprojects {
    apply plugin: 'java'
    apply plugin: 'license'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = 'de.johni0702.replay'
    version = rootProject.version
    ext.url = rootProject.authorUrl

    license {
        ext.name = rootProject.projectName
        ext.author = rootProject.author
        ext.url = rootProject.authorUrl
        ext.year = rootProject.inceptionYear
        header new File(rootProject.getProjectDir(), 'HEADER.txt')
        sourceSets = project.sourceSets
        ignoreFailures false
        strictCheck true
        mapping("java", "SLASHSTAR_STYLE")
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    build.dependsOn(sourcesJar)
    build.dependsOn(javadocJar)

    repositories {
        jcenter()
    }
}